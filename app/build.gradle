plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
}

def props = new Properties()
file('src/main/assets/config.properties').withInputStream {props.load(it)  }

android {
    compileSdk 30

    defaultConfig {
        applicationId "fi.climbstationsolutions.climbstation"
        minSdk 26
        targetSdk 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        buildTypes.each {
            it.buildConfigField 'String', 'CLIMBSTATION_URL', props.CLIMBSTATION_URL
            it.buildConfigField 'String', 'USERNAME', props.USERNAME
            it.buildConfigField 'String', 'PASSWORD', props.PASSWORD
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {
    // Room database
    def room_version = "2.3.0"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-runtime:$room_version"

    // Fragment
    def fragment_version = "1.3.6"
    implementation("androidx.fragment:fragment-ktx:$fragment_version")

    // Retrofit
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.1'

    // Lifecycle
    def lifecycle_version = '2.3.1'
    def arch_version = '2.1.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "android.arch.lifecycle:extensions:1.1.1"
    testImplementation "androidx.arch.core:core-testing:$arch_version"

    // ViewPager
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    // Navigation
    def nav_version = '2.3.5'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Moshi
    def moshi_version = '1.12.0'
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"

    // Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    def espresso_version = "3.3.0" // Use 3.3.0, because 3.4.0 is crashing
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_version"
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.3.1'
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2"
    debugImplementation "androidx.fragment:fragment-testing:$fragment_version"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // RecyclerView
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    // CameraX
    def camerax_version = "1.0.1"
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:1.0.0-alpha27"

    // QR-reader
    implementation 'com.google.mlkit:barcode-scanning:17.0.0'

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'

    //horizontal number picker
    implementation 'me.angrybyte.picker:picker:1.3.1'
}